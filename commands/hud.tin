#nop // Maintain a heads-up display

#var hud_file {logs/hud.log}

#nop // Define variables
#var stat_line1 {}
#var stat_line2 {}
#var sc_line1 {}
#var sc_line2 {}
#var tpl_stat_line1 {Your fatigue is %1; Your willpower is %2; Your statistics are}
#var tpl_stat_line2 {Str: %1, Int: %2, Wil: %3, Dex: %4, Con: %5, Lea: %6.}
#var tpl_sc_line1 {You have %1 hit, %2 stamina, %3 moves, %4 spirit.}
#var tpl_sc_line2 {OB: %1, DB: %2, PB: %3, Speed: %4, Gold: %5, XP Needed: %6.}

#alias gag_prompt {
	#act {^$} {
		#unact {^$};
		#line gag
	};

	#act {^>$} {
		#unact {^>$};
		#line gag
	}
}

#nop // Update the HUD
#alias hud_log {
	#line log $hud_file {$stat_line1\n$stat_line2\n\n$sc_line1\n$sc_line2}
};

#nop // Captures with gag
#alias capture_hud_gag {
	#nop // Capture `stat` command;
	#action {$tpl_stat_line1} {
		#line {gag};
		#var stat_line1 %%0;

		#nop // Capture without gag;
		#action {$tpl_stat_line1} {
			#var stat_line1 %%0
		}
	};

	#action {$tpl_stat_line2} {
		#var stat_line2 %%0;
		#line {gag};
		gag_prompt;

		#nop // Capture without gag;
		#action {$tpl_stat_line2} {
			#var stat_line2 %%0;
			hud_log
		};
	};

	#nop // Capture the `score` command;
	#action {$tpl_sc_line1} {
		#line {gag};
		#var sc_line1 %%0;

		#nop // Capture without gag;
		#action {$tpl_sc_line1} {
			#var sc_line1 %%0
		};
	};

	#action {$tpl_sc_line2} {
		#var sc_line2 %%0;
		#line {gag};
		gag_prompt;
		hud_log;

		#nop // Capture without gag;
		#action {$tpl_sc_line2} {
			#var sc_line2 %%0;
			hud_log
		};
	}
}

#nop // Run `stat` and `score` periodically
#alias update_hud {
	capture_hud_gag;
	stat;
	score
}
